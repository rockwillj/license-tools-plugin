// This project's rootProject is ":buildSrc", so it should have a copy of build-script settings of the "/build.gradle"
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.3.4' // https://github.com/novoda/bintray-release
    }
}
repositories {
    jcenter()
}

apply from: file('../metadata.gradle')

apply plugin: 'groovy'
apply plugin: 'maven' // to add "plugin:install" to install it locally
apply plugin: 'com.novoda.bintray-release' // to upload it to bintray

targetCompatibility = JavaVersion.VERSION_1_8
sourceCompatibility = JavaVersion.VERSION_1_8

final metadata = ext.metadata

// for "plugin:install"
group = metadata.groupId
jar.baseName = metadata.artifactId
version = metadata.version

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'com.android.tools.build:gradle:2.1.2'
    compile 'org.yaml:snakeyaml:1.17'
    compile 'org.apache.commons:commons-lang3:3.4'
    testCompile 'junit:junit:4.12'
}

publish {
    artifactId = metadata.artifactId
    desc = metadata.description

    userOrg = metadata.organization
    groupId = metadata.groupId
    publishVersion = metadata.version
    website = metadata.website
    repository = metadata.repository
    licences = metadata.licences

    bintrayUser = metadata.bintrayUser
    bintrayKey = metadata.bintrayKey
}
